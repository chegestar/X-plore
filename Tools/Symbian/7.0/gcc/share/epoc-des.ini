# User-defined functions to display EPOC descriptors
# Copyright 2000, 2001 Symbian Ltd.
#
# This file has been added to GDB by Symbian to support EPOC.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.

define epoc-print-des16 
	#
	# Display descriptor type and compute actual string location
	#

	set $pd_type=(($arg0).iLength >> 28) & 0xF

	printf "Type: "

	set $pd_ptr=0

	if $pd_type == 0
		echo TBufC16 or HBufC16\n
		set $pd_ptr=((TText16*) (((TBufCBase16*) &($arg0)) + 1))
	end

	if $pd_type == 1
		echo TPtrC16\n
		set $pd_ptr=((TPtrC16*) &($arg0))->iPtr
	end

	if $pd_type == 2
		echo TPtr16\n
		set $pd_ptr=((TPtr16*) &($arg0))->iPtr
	end

	if $pd_type == 3
		echo TBuf16\n
		set $pd_ptr=((TText16*) (((TBufBase16*) &($arg0)) + 1))
	end

	if $pd_type == 4
		echo TPtr16 pointing to a HBufC instance\n
		set $pd_ptr=((HBufC16*) ((TPtr16*) &($arg0))->iPtr).iBuf
	end

	#
	# Display length and content unless bad descriptor
	#

	if $pd_ptr == 0
		echo Unknown (probably not a valid descriptor)\n
	else
		set $pd_len=($arg0).iLength & 0x0FFF
		printf "Length = %d\n", $pd_len
	
		printf "Content = \""
		set $pd_i=0
		while $pd_i < $pd_len
			set $pd_ch=($pd_ptr)[$pd_i++]
			if 32 <= $pd_ch && $pd_ch < 127
				if $pd_ch == '"'
					printf "\\"
				end
				printf "%c", $pd_ch
			else
				printf "\\u%04X", $pd_ch
			end
		end
		printf "\"\n"
	end
end

document epoc-print-des16
	Print unicode descriptor info, (type, length and content)
	usage: epoc-print-des16 <<expression resulting in an EPOC descriptor>>
	Printable ASCII characters are printed directly (except the double quote 
	character which is printed prefixed by a backslash).
	Other characters are printed as \uXXXX where XXXX is an hexadecimal 
	number.
end


define pd16
	epoc-print-des16 ($arg0)
end

document pd16
	alias for epoc-print-des16	
end